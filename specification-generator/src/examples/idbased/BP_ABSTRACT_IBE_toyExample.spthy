theory BP_ABSTRACT_IBE_toyExample
begin

/*
 * Source: Toy Example showing our abstract IBE modeling in practice
 * Author: Lucca Hirschi & Ralf Sasse
 * Date: November 2018
 *
 * Status: Automatic proofs/attacks of challenge secrecy and non-injective agreement from initiator + compromised scenarios.
 * Protocol: Alice --{<Alice,Bob,challenge>}_Bob-->    Bob
 *           Bob   --{<challenge,'response'>}_Alice--> Alice
 */


functions: IBPriv/2, IBPub/2, GetIBMasterPublicKey/1,  // [same for IBS]
           idenc/2, iddec/2                            // [almost the same for IBS]
equations: iddec(idenc(plaintext,IBPub(A, GetIBMasterPublicKey(IBMasterPrivateKey))), // [almost the same for IBS]
                 IBPriv(A, IBMasterPrivateKey))
           = plaintext


/**** Setup Key Escrow [same for IBS] ****/
// Create the trusted entity holding the master private key (only once)
rule create_IB_PrivateKeyGenerator:
  [ Fr(~IBMasterPrivateKey) ]
  --[ Once('PKG') ]->  // ['PKG' can be replaced by a different name or $PKG in case there are multiple PKGs]
  [ !IB_MasterPrivateKey('PKG', ~IBMasterPrivateKey)
  , Out(<'PKG', GetIBMasterPublicKey(~IBMasterPrivateKey)>) // the attacker learns the master pulic key
  ]


/**** Initialization rules for identities [same for IBS] ****/
rule create_IB_identity:
  let Master_pk = GetIBMasterPublicKey(IBMasterPrivateKey)  // master public key
      User_sk = IBPriv($A, IBMasterPrivateKey)              // user secret key
  in
  [ !IB_MasterPrivateKey('PKG', IBMasterPrivateKey)
  , Fr(~id)
  ]
  --[ CreateId($A, <Master_pk, User_sk>) ]->
  [ !IB_Identity(~id, $A, Master_pk, User_sk) ]


/**** Reveal rules modelling compromise scenarios [same for IBS] ****/
// Reveals the identity based master private key
rule Reveal_IB_MasterPrivateKey:
  [ !IB_MasterPrivateKey(PKG, IBMasterPrivateKey) ] --[ Reveal('MASTER_KEY',PKG) ]-> [ Out(IBMasterPrivateKey) ]

// Reveals the identity based private key of an agent A
rule Reveal_IB_privkey:
  [ !IB_Identity(~id, A, Master_pk, User_sk) ] --[ Reveal('USER_KEY',A) ]-> [ Out(User_sk) ]


/**** Protocol rules [almost the same for [IBS] ****/
rule Alice_send:
let bobKey = IBPub('Bob', Master_pk)
    plaintext = <'Alice', 'Bob', ~challenge>
    mOut = idenc(plaintext, bobKey)
  in
  [ !IB_Identity(~id, 'Alice', Master_pk, User_sk)
  , Fr(~challenge)
  ]
  --[ Secret(~challenge)
    , Step()
    ]->
  [ Out(mOut)
  , St_Alice_0(~id, Master_pk, User_sk, ~challenge)
  ]

rule Bob_recv:
  let plaintext = iddec(mIn, User_sk)
      sender = fst(plaintext)
      recipient = fst(snd(plaintext))
      challenge = snd(snd(plaintext))
      aliceKey = IBPub('Alice', Master_pk)
      mOut = idenc(<challenge, 'response'>, aliceKey)
  in
  [ !IB_Identity(~id, 'Bob', Master_pk, User_sk)
  , In(mIn)
  ]
--[ Eq(<sender,recipient>,<'Alice','Bob'>)
  , Running('Bob', 'Alice', <'Responder', 'Initiator', challenge>)
  ]->
  [ Out(mOut) ]

rule Alice_recv:
  let plaintext = iddec(mIn, User_sk)
  in
  [ St_Alice_0(~id, Master_pk, User_sk, ~challenge)
  , In(mIn)
  ]
--[ Eq(plaintext, <~challenge,'response'>)
  , Commit('Alice', 'Bob', <'Initiator', 'Responder', ~challenge>)
  , Full()
  ]->
  [ ]

/**** Restrictions ****/
restriction Equality:
  "All x y #i. Eq(x,y) @i ==> x = y"

restriction Once:
  "All X #i #j. Once(X)@i & Once(X)@j ==> #i = #j"


/**** Lemmas ****/
// [HOLDS] Challenge secrecy
lemma secrecy_session_key:
  "All s #i. 
    Secret(s) @i ==> (
        not (Ex #j. K(s)@j)
      | (Ex #l. Reveal('USER_KEY','Alice')@l)
      | (Ex #l. Reveal('USER_KEY','Bob')@l)
      | (Ex #l. Reveal('MASTER_KEY','PKG')@l))"

// [HOLDS] Non-injective agreement from Initiator with compromised Alice
lemma nonInjectiveAgreementChallenge_Initiator_Acompro:
  "All a b t #i. 
    Commit(a,b,<'Initiator','Responder',t>) @i
    ==> (Ex t #j. Running(b,a,<'Responder','Initiator',t>) @j)
        | (Ex #r. Reveal('USER_KEY', b)@r)
        | (Ex #j. Reveal('MASTER_KEY', 'PKG')@j)"

// [FAIL] Non-injective agreement from Initiator with compromised Alice and Bob
lemma nonInjectiveAgreementChallenge_Initiator_ABcompro:
  "All a b t #i. 
    Commit(a,b,<'Initiator','Responder',t>) @i
    ==> (Ex t #j. Running(b,a,<'Responder','Initiator',t>) @j)
        | (Ex #j. Reveal('MASTER_KEY', 'PKG')@j)"


// [HOLDS] Sanity check
lemma executability_lemma:
  exists-trace
  " Ex #i. Full()@i
  & not(Ex X Y #r. Reveal(X, Y)@r)"

end