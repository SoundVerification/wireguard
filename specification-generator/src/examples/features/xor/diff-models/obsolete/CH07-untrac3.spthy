theory CH07_untrac3 begin

// This model allows picking each reader and tag's identity from the
// pool of identities very late, which allows everything to be easily
//mirrored in a silly way. This model is not sensible and removed.

builtins: xor, hashing, multiset
functions: rot/2, lh/1, rh/1

rule Setup:
   let left = <~k1,~id1>+<~k2,~id2>
      right = <~k1,~id1>+<~k1,~id1>
   in
   [ Fr( ~k1 )
   , Fr( ~id1 )
   , Fr( ~k2 )
   , Fr( ~id2 ) 
   ]
 --[ OnlyOnce( ) ]-> 
   [ !Reader( '1'+diff(left,right) )
   , !Tag(    '1'+diff(left,right) )
   ]

rule reader1:
   [ Fr( ~r1 ) ]
 --[ Challenge( ~r1, 'Reader' ) ]->
   [ Out( ~r1 )
   , Initiated( ~r1 ) 
   ]

rule tag1:
   let hash = h((r1⊕~r2⊕~k)) in 
   [ In( r1 )
   , Fr( ~r2 )
   , !Tag( < ~k, ~id > + z ) 
   ]
 --[ Response( ~k, 'Tag' )
   , Challenge(~r2, 'Tag') 
   ]->
   [ Out( <~r2,lh(rot(~id,hash))>)
   , TagState(~k, ~id, r1, ~r2, hash)
   ]

rule reader2:
   [ Initiated( ~r1 )
   , !Reader( < ~k, ~id > + z ) 
   , In( <r2, lh(rot(~id,hash))> ) 
   ]
 --[ Alive( ~k, 'Tag' )
   , Response(~k, 'Reader') 
   , Eq( h((~r1⊕r2⊕~k)), hash )
   ]->
   [ Out( rh(rot(~id,hash)) ) ]

rule tag2:
   [ In( rh(rot(~id,hash)) )
   , TagState(~k, ~id, r1, ~r2, hash)
   ]
 --[ Alive( ~k, 'Reader' ) ]->
   [ ]

restriction equality:
  "∀ x y #i. (Eq( x, y ) @ #i) ⇒ (x = y)"

/*
restriction max_number_of_tags_tag:
  "All a b c d #i. Tag(a+b+c+d)@i ==> F"

restriction max_number_of_tags_tag_reader:
  "All a b c d #i. Reader(a+b+c+d)@i ==> F"
*/

restriction unique:
  "∀ #i #j. ((OnlyOnce( ) @ #i) ∧ (OnlyOnce( ) @ #j)) ⇒ (#i = #j)"

// There should be an attack on this
// FINDS ATTACK AUTOMATICALLY (2017-11-01, aa65be43cd4965ffa1c51a991d9139e98ec1662e) [left+right, using a 'zero', but with some clicking can get a "more realistic" attack easily]
lemma recentalive_tag:
  all-traces "∀ x #i. (Alive( x, 'Tag' ) @ #i) ⇒ (∃ y #j #k. Challenge(y, 'Reader') @k & Response( x, 'Tag' ) @ #j & k < j & j < i)"

// FINDS PROOF AUTOMATICALLY (2017-11-01, aa65be43cd4965ffa1c51a991d9139e98ec1662e) [left+right]
lemma recentalive_reader:
  all-traces "∀ x #i. (Alive( x, 'Reader' ) @ #i) ⇒ (∃ y #j #k. Challenge(y, 'Tag') @k & Response( x, 'Reader' ) @ #j & k < j & j < i)"

// FINDS PROOF AUTOMATICALLY (2017-11-01, aa65be43cd4965ffa1c51a991d9139e98ec1662e) [left+right]
lemma executable:
  exists-trace "∃ x #i #j. (Alive( x, 'Reader' ) @ #i) ∧ (Response( x, 'Tag' ) @ #j)"

// FINDS PROOF AUTOMATICALLY, about 240 minutes (2017-11-01, aa65be43cd4965ffa1c51a991d9139e98ec1662e)
// observational equivalence lemma

end
